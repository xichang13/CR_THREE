- [宏汇编语法](#宏汇编语法)
  - [表达式](#表达式)
  - [算术表达式](#算术表达式)
  - [逻辑运算](#逻辑运算)
  - [关系运算符](#关系运算符)
  - [标号](#标号)
    - [匿名标号](#匿名标号)
    - [调整偏移量指令 ORG](#调整偏移量指令-org)
    - [当前地址指令 $](#当前地址指令-)
  - [结构体](#结构体)
  - [宏](#宏)
    - [equ 语句](#equ-语句)
    - [= 语句](#-语句)
    - [macro 语句](#macro-语句)
    - [伪指令分支语句](#伪指令分支语句)
- [多文件编译](#多文件编译)
  - [源文件](#源文件)
  - [头文件](#头文件)
  - [全局变量](#全局变量)
  - [编译](#编译)

# 宏汇编语法

## 表达式

表达式中的求值是在程序链接时完成的，所以表达式中的各值必须是在汇编或链接期就能确定，也就是说不能将寄存器或者变量运用于表达式。

## 算术表达式

| 运算符 | 意义 | 例子 |
| --- | --- | --- |
| + | 加法 | 1 + 2 |
| - | 减法 | 1 - 2 |
| * | 乘法 | 1 * 2 |
| / | 除法 | 1 / 2 |
| mod | 取模 | 1 mod 2 |

``` asm
mov ax, 1 + 2
mov ax, 1 - 2
mov ax, 1 * 2
mov ax, type byte * 2
mov ax, 1 mod 2
mov ax, 1 / 2
mov ax, size g_buf / type g_buf
```

## 逻辑运算

位运算，与对应的指令相同，在操作码部分是操作码，在操作数是逻辑运算符。

| 运算符 | 意义 |
| --- | --- |
| and | 与 |
| or | 或 |
| xor | 异或 |
| not | 取反 |

``` asm
mov ax, 1 and 2
mov ax, 1 or 2
mov ax, 1 xor 2
mov ax, not 1
```

## 关系运算符

关系运算的结果，如果结果为真，则所有位都为1，即FFFF，否则都为0，即0000。

| 运算符 | 英文 | 意义 |
| --- | --- | --- |
| EQ | equal | == 等于 |
| NE | not equal | != 不等于 |
| GT | greater than | > 大于 |
| LT | less than | < 小于 |
| GE | greater than or equal | >= 大于等于 |
| LE | less than or equal | <= 小于等于 |

``` asm
mov ax, 1 EQ 2
mov ax, 1 NE 2
mov ax, 1 GT 2
mov ax, 1 LT 2
mov ax, 1 GE 2
mov ax, 1 LE 2
```

## 标号

### 匿名标号

* `@@` 是匿名标号
* `@b` 向上查找最近的 `@@` ，back
* `@f` 向下查找最近的 `@@` ，front

``` asm
@@: mov ax, 1
    jmp @b ; 跳到第一行
    mov ax, 2
@@: mov ax, 3
    jmp @f ; 跳到第七行
    mov ax, 4
@@: mov ax, 5
```

### 调整偏移量指令 ORG

* 格式 `ORG offset`
* 此指令后的下一个变量或指令从`offset` 开始存放

``` asm
data segment
    g_buf db 10 dup(0)
    org 05h
    g_w dw 1122h ; g_w 从 05h 开始存放
data ends

code segment
entry:
    org 100h
    mov ax, 1122h ; 此指令从代码段偏移 100h 开始存放

    mov ax, 4c00h
    int 21h
code ends
    end entry
```

### 当前地址指令 $

* `$` 表示当前指令或变量的地址
* 常用于计算缓冲区长度和获取当前 ip 值
* 可与 `ORG` 指令配合使用

``` asm
data segment
    g_buf db 10 dup(0)
    g_len dw $ - g_buf ; g_len 存放 g_buf 的长度
    org $ + 10h ; 下一条指令从当前地址 + 10h 开始存放
    g_w dw 1122h
data ends

code segment
entry:
    org 100h
    mov ax, 1122h ; 此指令从代码段偏移 100h 开始存放

    mov ax, $ ; 获取当前 ip 值
    mov ax, $ + 4 ; 获取当前 ip + 4 值

    jmp $ + 3;
    db oebh ; 花指令

    ; 运行时修改指令
    mov bx, $ + 11h
    mov byte ptr cs:[bx], 0b8h
    mov byte ptr cs:[bx + 1], 00h
    mov byte ptr cs:[bx + 2], 4ch

    nop
    nop
    nop
    nop

code ends
    end entry
```

## 结构体

``` asm
结构体名 struc
    字段1 类型
    字段2 类型
结构体名 ends
```

* 结构体使用 `<>` 初始化
* 结构体可以通过变量名和寄存器来访问成员

``` asm
student struc
    m_name db 10 dup(0)
    m_id dw ?
student ends

data segment
    g_stu student <'zhangsan', 1234h> ; 结构体全局变量
data ends

code segment
Func proc
    local @stu: student ; 结构体局部变量
    mov @stu.m_id, 5678h ; 访问成员

    ; 第二种方法
    assume bx:ptr student; 通过指针
    lea bx, @stu
    mov [bx].m_id, 5678h
    ret
Func endp

entry:
    assume ds:data, cs:code
    mov ax, data
    mov ds, ax
    mov es, ax

    mov g_stu.m_id, 5678h ; 访问成员
    lea bx, g_stu.m_name ; 访问成员

    mov ax, 4c00h
    int 21h
code ends
    end entry
```

## 宏

### equ 语句

* 不可以重命名
* 可用于常量和表达式
* 可用于字符串
* 可用于指令名，给指令取别名
* 可用于类型，给类型取别名
* 可用于操作数

``` asm
COUNT equ 100h ; 常量
EXPRESSION equ COUNT + 1 ; 表达式
STRING equ "abc" ; 字符串
MOVE equ mov ; 指令别名
MYBYTE equ byte ; 类型别名
```

### = 语句

* 可以被修改
* 只能用于常数

``` asm
COUNT = 100h ; 常量
COUNT = 200h ; 修改常量
```

### macro 语句

``` asm
宏名 macro [参数1], [参数2], ...
    语句1
    语句2
    ...
endm
```

* 宏会在使用的地方展开
* 宏参数可以省略
* 字符串拼接使用 `&`

``` asm
; 宏定义
movm macro reg1, reg2
    push reg1
    pop reg2
endm

; 拼接
shift macro n, reg, d
    mov cl, n
    ro&d reg, cl
endm

data_seg segment
    g_w1 dw 4
    g_w2 dw 5
data_seg ends

CODE segment

START:
    assume ds:data_seg, ss:stack_seg
    mov ax, data_seg
    mov ds, ax

    lea si, g_w1
    lea di, g_w2
    movm word ptr [si], word ptr [di]
    movm word ptr [bx], word ptr [bp]
    shift 4, ax, l
    shift 4, ax, r

    mov ax, 4c00h
    int 21h
CODE ends
    end START
```

### 伪指令分支语句

``` asm
; 分支指令
.if ax == 1
    mov bx, 1
.elseif ax == 2
    mov bx, 2
.else
    mov bx, 3
.endif

; 循环指令
.while condition
    语句
.endw
```

# 多文件编译

## 源文件

* 源文件后缀 `.asm`
* 每个源文件末尾需要有 `end` ，但是入口只能有一个
* 访问其他文件的段，需要在段后加 `public` ，将段合并

## 头文件

* 头文件后缀 `.inc`
* `include xxx.inc`
* 头文件防止重复包含

``` inc
; 头文件
ifndef SECOND_1
SECOND_1 equ 1

; 全局变量使用 extern 声明
extern g_w:word 

; 声明函数
;Func proto far stdcall arg1:word, arg2:word
Func proto stdcall arg1:word, arg2:word

endif
```

``` asm
; 源文件

; 全局变量使用 public 定义
public g_w
; 变量
data segment public
    g_w dw 1234h
data ends

; 函数定义
code segment public
    ; Func proc far stdcall arg1:word, arg2:word
    Func proc stdcall arg1:word, arg2:word
        mov ax, arg1
        mov ax, arg2
        ret
    Func endp
code ends
end
```

**函数使用**

函数在源文件中定义，在头文件中声明即可。

## 全局变量

* 全局变量在定义文件中必须使用 `public` 指明此变量为全局 `public 变量名`
* 全局变量在引用文件中必须使用 `extern` 指明此变量为全局 `extern 变量名`

## 编译

* `ml /c xxx.asm yyy.asm` 编译源文件
* `link xxx.obj yyy.obj` 链接源文件

或者

* `ml *.asm` 编译所有源文件